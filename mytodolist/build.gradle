buildscript {
	ext {
		springBootVersion = '1.5.16.BUILD-SNAPSHOT'
		querydslVersion='4.2.1'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.franzbecker:gradle-lombok:1.8")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.franzbecker.gradle-lombok'



group = 'com.hschoi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

/**** QueryDSL Class Generate Script ****/
def queryDslOutput = file("hschoi/main/java")

sourceSets {
    main.java.srcDirs=['src/main/java', 'hschoi/main/java']
    main.resources.srcDirs=['src/main/resources']
}

task generateQueryDSL(type: JavaCompile, group: 'build') {
    doFirst {
        if (!queryDslOutput.exists()) {
            logger.info("Creating `$queryDslOutput` directory")

            if (!queryDslOutput.mkdirs()) {
                throw new InvalidUserDataException("Unable to create `$queryDslOutput` directory")
            }
        }
    }

    source = sourceSets.main.java
    classpath = configurations.compile
    options.compilerArgs = [
            "-proc:only",
            /********************* 핵심 포인트! *********************/
            "-processor",
            'com.querydsl.apt.jpa.JPAAnnotationProcessor,lombok.launch.AnnotationProcessorHider$AnnotationProcessor'
    ]
    destinationDir = queryDslOutput
}

compileJava.dependsOn(generateQueryDSL)
compileTestJava.dependsOn(generateQueryDSL)

clean {
    delete queryDslOutput
}


repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-aop')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-jdbc')
	compile('org.springframework.boot:spring-boot-starter-web')
	runtime('com.h2database:h2')
	runtime('mysql:mysql-connector-java')
	compile ("ch.simas.qlrm:qlrm:1.7.1")
    compile ('org.modelmapper:modelmapper:2.1.0')
	compileOnly('org.projectlombok:lombok')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	/**
     * querydsl dependency
     */
	compile ("com.querydsl:querydsl-core:${querydslVersion}")
    compile ("com.querydsl:querydsl-apt:${querydslVersion}")
    compile ("com.querydsl:querydsl-jpa:${querydslVersion}")
    compile ("com.querydsl:querydsl-sql:${querydslVersion}")

}
